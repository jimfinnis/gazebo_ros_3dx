# generates sonar positions from a table, obtained by inspecting
# the model in Blender. Outputs URDF. Also outputs C++ code to
# /tmp/foo

`regex library drop
`io library drop
require "util.ang" drop
require "xmlgen.ang" drop

# table of half the front sonars
--EOT
0       0.0677  0.1404      90
1       0.1172  0.1229      50
2       0.1517  0.0799      30
3       0.1699  0.0297      10
--EOT

# base_link position offsets from .xacro file
0 const XOFF
0 const ZOFF
# Z position of all sonars
0.2084 const ZPOS

3.1415927 const PI


# modify a hash by applying a function to a key
:hashmod |key,func,hash:|
    ?key ?hash get ?func@ ?key ?hash set
;

(
    # read and parse the data
    "\n" split
    []!L
    each {
        i len 10 > if
            i <<"\\s+" regex$compile>> regex$split
            (tofloat) map
            explode !Angle !Y !X !Num
            [% `x ?X,`y ?Y,`z ZPOS,`a ?Angle] ?L push
        then
    }
    # add the other side of the sensor, which is a copy
    # reflected in Y and angle, and reversed.
    ?L clone !L2
    ?L2 reverse each {
        i clone !H 
        `y (neg) ?H hashmod
        `a (neg) ?H hashmod
        ?H ?L push
    }
    # Nice. Now add the offsets
    ?L each {
        `x (XOFF +) i hashmod
        `z (ZOFF +) i hashmod
    }
    
    # Output as sonar .urdf
    0!Ct
    `robot
    [%
     "xmlns:sensor" "http://playerstage.sourceforge.net/gazebo/xmlschema/#sensor",
     "xmlns:interface" "http://playerstage.sourceforge.net/gazebo/xmlschema/#interface",
     "xmlns:xacro" "http://ros.org/wiki/xacro"
     ]
    
    "/tmp/foo" "w" io$open !PlainOut
    
    [
    ?L each {
        "s" ?Ct + !Name
        [i?`x, i?`y, i?`z] "%f %f %f" format !Origin
        [i?`a PI * 180.0 /] "0 0 %f" format !Rot
        
        # also output as C++
        [?Ct,i?`x, i?`y,i?`a PI * 180.0 /]
        "sonars[%d].set(%f,%f,%f);\n" format ?PlainOut io$write
        ?Ct 1+ !Ct
        
        
        # build XML tags
        "xacro:sonar_sensor"
        [%
         `name ?Name,
         `parent "base_link",
         `ros_topic ?Name,
         `update_rate 10,
         `min_range 0.1,
         `max_range 3,
         `field_of_view 0.267,
         `ray_count 2
         ]
        [
         "origin"
         [%
          `xyz ?Origin,
          `rpy ?Rot
          ]
         none xmlgen$mktag
         ]
        xmlgen$mktag,
    }
    xmlgen$mktag?`output@.
         
    0!PlainOut
        
)@ quit
    
